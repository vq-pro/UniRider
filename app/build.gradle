plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {

    compileSdkVersion 31
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "quebec.virtualite.unirider"
        minSdkVersion 28
        multiDexEnabled true
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        testApplicationId "quebec.virtualite.unirider.test"
        testInstrumentationRunner "quebec.virtualite.unirider.test.CucumberInstrumentationRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "version"

    kotlinOptions {
        jvmTarget = '1.8'
    }

    productFlavors {
        end2end {
            buildConfigField 'String', 'SCENARIOS', '"~@Integration"'

            buildConfigField 'Boolean', 'BLUETOOTH_ACTUAL', 'true'

            // Calls the backend
            // Equivalent to localhost, which doesn't work with unmocked Retrofit2 request
            buildConfigField 'String', 'SERVER_BASE_URL', '"http://10.0.2.2:8080"'
            buildConfigField 'Boolean', 'SERVER_ACTUAL', 'true'
        }

        integration {
            buildConfigField 'String', 'SCENARIOS', '"~@End2End"'

            buildConfigField 'Boolean', 'BLUETOOTH_ACTUAL', 'false'

            // Backend is mocked
            buildConfigField 'String', 'SERVER_BASE_URL', '"http://localhost:8080"'
            buildConfigField 'Boolean', 'SERVER_ACTUAL', 'false'
        }
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
            java.srcDirs = ['src/androidTest/kotlin']
        }

        main {
            java.srcDirs = ['src/main/kotlin']
        }

        test {
            java.srcDirs = ['src/test/kotlin']
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'

    // Database
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Retrofit
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Unit Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.objenesis:objenesis:3.2'

    // Integration Tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // Cucumber
    androidTestImplementation 'info.cukes:cucumber-android:1.2.6'
    androidTestImplementation 'info.cukes:cucumber-picocontainer:1.2.6'

    // MockWebServer
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'

    // Apache HttpCore
    implementation 'org.apache.httpcomponents:httpcore:4.4.15'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.1'
}
